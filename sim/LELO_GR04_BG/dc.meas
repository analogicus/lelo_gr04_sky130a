* Measure LELO_GR04_BG
.control

load {cicname}.raw

echo "MEAS_START"

* Get currents between -45 to 125
let temp_sweep = v(vtemp)
let temp_start = -45
let temp_end = 125

let sweep_start = temp_sweep[0]
let sweep_end = temp_sweep[length(temp_sweep) - 1]
let sweep_step = (sweep_end - sweep_start) / (length(temp_sweep) - 1)
let idx_start = floor((temp_start - sweep_start) / sweep_step + 0.5)
let idx_end = floor((temp_end - sweep_start) / sweep_step + 0.5)

print temp_sweep[idx_start]
print temp_sweep[idx_end]

* PMOS-generated current: positive
let i0_filtered = i(v0)[idx_start, idx_end]
let i1_filtered = i(v1)[idx_start, idx_end]
let i2_filtered = i(v2)[idx_start, idx_end]
let i3_filtered = i(v3)[idx_start, idx_end]
let temp_filtered = temp_sweep[idx_start, idx_end]
let len = length(temp_filtered)

* Get temperature range
let tmax = maximum(temp_filtered)
let tmin = minimum(temp_filtered)
let trange = tmax - tmin

* Get current coefficient with temperature
let a0 = (i0_filtered[len - 1] - i0_filtered[0])/trange
let a1 = (i1_filtered[len - 1] - i1_filtered[0])/trange
let a2 = (i2_filtered[len - 1] - i2_filtered[0])/trange
let a3 = (i3_filtered[len - 1] - i3_filtered[0])/trange

* Get index & offset at 25C
let index_25 = floor((25 - temp_start) / sweep_step + 0.5) - 1
print temp_filtered[index_25]

let offset0_25 = i0_filtered[index_25]
let offset1_25 = i1_filtered[index_25]
let offset2_25 = i2_filtered[index_25]
let offset3_25 = i3_filtered[index_25]

* Get min/max/avg of coefficients & offsets
compose as values a0 a1 a2 a3
compose offsets_25 values offset0_25 offset1_25 offset2_25 offset3_25
let a_avg = (maximum(as) + minimum(as))/2
let a_range = maximum(as) - minimum(as)
let offset_avg_25 = (maximum(offsets_25) + minimum(offsets_25))/2
let offsets_range_25 = maximum(offsets_25) - minimum(offsets_25)

print a_avg
print offsets_range_25

* Get reference and errors for Vin = 0.5V & calibrated at 25C
let i1_ref = (temp_filtered - temp_filtered[index_25]) * a1 + offset1_25
let i1_err = i1_filtered - i1_ref
let i1_err_max = maximum(i1_err)
let i1_err_min = minimum(i1_err)
let i1_25 = i1_filtered[index_25]

print i1_err_max
print i1_err_min
print i1_25

* Get reference and errors for Vin = 0.8V & calibrated at 25C
let i2_ref = (temp_filtered - temp_filtered[index_25]) * a2 + offset2_25
let i2_err = i2_filtered - i2_ref
let i2_err_max = maximum(i2_err)
let i2_err_min = minimum(i2_err)
let i2_25 = i2_filtered[index_25]

print i2_err_max
print i2_err_min
print i2_25

echo "MEAS_END"
.endc
